# Wotif modifications: Justin Dray <justin.dray at wotifgroup dot com>
# Maintainer:  Patrick McCarty <pnorcks at gmail dot com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Thomas S Hatch <thatch45 ar gmail dot com>
# Contributor: Michael P <ptchinster@archlinux.us>
# Contributor: Matt Heagney <matt@heagney.com>

pkgname=multipath-tools-wotif
pkgver=0.4.9
pkgrel=14
pkgdesc='Multipath tools for Linux'
arch=('x86_64')
url="http://christophe.varoqui.free.fr/"
license=('GPL')
depends=('bash' 'libaio' 'device-mapper')
backup=('etc/multipath.conf' 'etc/multipath.conf.annotated')
install=multipath-tools.install
provides=("kpartx=$pkgver")
options=(!emptydirs)
source=(http://christophe.varoqui.free.fr/multipath-tools/multipath-tools-$pkgver.tar.bz2
	service
	multipath.conf
	multipath.conf.annotated
        fix-build.patch
	buffer-overflows.patch
	log_enquery_overflow.patch
        blacklist-cciss-devices.patch
	explicitly-include-posix_types.h.patch)
md5sums=('a6d4b48afc28f1f50f5ee4b1b06d2765'
         '4843e91a83660e4b3acfb2d804fa344e'
         '3f9bc7acfee557b74669bf40618c2974'
         'be11462922eeeb9fcd2ba5f3f137b7d9'
         '885c0ba9c90b73cc93aa3f78005f81d6'
         'c5aab36777b0304a3525533cdd31bddc'
         '00eae05e02f1b85062e998574ab1b833'
         '61b4038c4c145ca52e836145ea6bdd6c'
         'd99beb6d8a9b84f90125c7cacf8a6006')

build() {
  patch -Np1 -i fix-build.patch
  patch -Np1 -i buffer-overflows.patch
  patch -Np1 -i log_enquery_overflow.patch
  patch -Np1 -i blacklist-cciss-devices.patch
  patch -Np1 -i explicitly-include-posix_types.h.patch

  # Needs to be fixed upstream. Refer to
  # https://bbs.archlinux.org/viewtopic.php?pid=793814#p793814
  export LDFLAGS=${LDFLAGS/-Wl,--as-needed}
  
  make LIB='usr/lib'
}

package() {
  make LIB='usr/lib' \
       DESTDIR="$pkgdir" \
       bindir='/usr/bin' \
       libudevdir='/usr/lib/udev' install

  install -Dm644 multipath.conf "$pkgdir"/etc/multipath.conf
  install -Dm644 multipath.conf.annotated "$pkgdir"/etc/multipath.conf.annotated
  install -Dm644 service "$pkgdir"/usr/lib/systemd/system/multipathd.service

  sed -i 's/sbin/bin/g' "$pkgdir"/usr/lib/udev/rules.d/*.rules
}
