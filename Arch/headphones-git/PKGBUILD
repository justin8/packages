# Maintainer: Justin Dray <justin at dray dot be>
# Contributor: Slash <demodevil5 at yahoo dot com>
# Contributor: blades <blades at gecko dot org dot uk>
# Contributor: jonkristian <hello at jonkristian dot no>

pkgname=headphones-git
pkgver=v0.4.r11.g3f5044c
pkgrel=2
epoch=1
pkgdesc="Automatic music downloader for SABnzbd"
arch=('any')
url="http://github.com/rembo10/headphones"
license=('GPL3')
depends=('python2')
backup=('etc/conf.d/headphones')
conflicts=('headphones')
replaces=('headphones')
install='headphones.install'
source=("git+https://github.com/rembo10/headphones.git"
	'headphones.service'
	'headphones.conf')
md5sums=('SKIP'
         '1eb5452eb3755e9360156bdb01331092'
         '005b3a5615c87841988bebb9fdfe0bb8')

pkgver() {
	cd "$srcdir/headphones"
	git describe --long --tags | sed -r 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
	find "$srcdir" -iname '*.py' -exec sed -i 's/#!\/usr\/bin\/env python.*$/#!\/usr\/bin\/env python2/;s/#!\/usr\/bin\/python.*/#!\/usr\/bin\/env python2/' {} \;
}

package() {
	install -d "${pkgdir}/opt/"
	cp -r "${srcdir}/headphones" "${pkgdir}/opt/headphones"

	install -D -m644 "${srcdir}/headphones.conf" "${pkgdir}/etc/conf.d/headphones"
	install -D -m644 "${srcdir}/headphones.service" "${pkgdir}/usr/lib/systemd/system/headphones.service"
}
