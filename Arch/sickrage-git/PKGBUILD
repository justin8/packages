# Maintainer: Justin Dray <justin@dray.be>

pkgname=sickrage-git
pkgver=r508.c873b86
pkgrel=1
pkgdesc="A PVR application that downloads and manages your TV shows. Echel0n fork of sickbeard, with tvrage and torrents support."
arch=('any')
url="https://github.com/echel0n/SickRage"
license=('GPL3')
depends=('python2' 'python2-cheetah')
makedepends=('git')
optdepends=('sabnzbd: NZB downloader'
            'python2-notify: desktop notifications')
provides=('sickbeard-tvrage-git')
conflicts=('sickbeard' 'sickbeard-piratebay-git' 'sickbeard-tvrage-git')
replaces=('sickbeard-tvrage-git')
options=('!strip')
install='sickrage.install'
source=("$pkgname::git://github.com/echel0n/SickRage.git"
        'sickrage-system.service'
        'sickrage-user.service'
	'sickrage.tmpfile')
sha256sums=('SKIP'
            '6d86f33a49394828626a33f36d0392a3a85243eda79ee474bbbd1d066f9bdb37'
            'c78db64732dc5f725868aea3e84f5ecc0fefef8fe7b5cbd419811929d0602714'
            '7fbf425e4ef14345e9a91c098c6f1a9cb6ddaed2617b04160a47d35747083565')

pkgver() {
	cd $pkgname
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
	mkdir -p "$pkgdir/opt/sickrage"
	chmod 775 "$pkgdir/opt/sickrage"
	cp -r $pkgname/* "$pkgdir/opt/sickrage"

	install -D -m644 sickrage-system.service "$pkgdir/usr/lib/systemd/system/sickrage.service"
	install -D -m644 sickrage-user.service "$pkgdir/usr/lib/systemd/user/sickrage.service"
	install -D -m644 sickrage.tmpfile "$pkgdir/usr/lib/tmpfiles.d/sickrage.conf"

	find "$pkgdir" -type d -name '.git' -exec rm -r '{}' +
}

