# Maintainer: Justin Dray <justin@dray.be>
# Contributor: DonVla <donvla@users.sourceforge.net>
# Contributor: mirandir <mirandir@orange.fr>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>

pkgname=skype4pidgin
pkgver=653
pkgrel=1
epoch=1
pkgdesc="A Skype plugin for libpurple clients (Pidgin, Empathy, Finch, etc)"
arch=("i686" "x86_64")
url="http://skype4pidgin.googlecode.com/"
license=('GPL3')
depends=('libpurple' 'skype')
makedepends=('pkgconfig' 'subversion')
optdepends=('pidgin' 'empathy' 'finch')
conflicts=('skype4pidgin-svn' 'skype4pidgin-svn-dbus' 'skype4finch' 'skype4finch-svn' 'skype4empathy' 'skype4empathy-svn')
replaces=('skype4finch' 'skype4empathy')
source=("$pkgname::svn+http://skype4pidgin.googlecode.com/svn/trunk/"
        'Makefile.patch'
	'https://sync.dray.be/public/packages/skype_icons_empathy.tar.xz')
md5sums=('SKIP'
         '8848134ab5babbc46549ae627365ff20'
         '5728aa01ac674fca2c01348719fe8672')

build() {
	cd "${pkgname}"
	svn up -r ${pkgver}
	patch -Np0 -i "${srcdir}/Makefile.patch"
	make all
}

package() {
	cd "${pkgname}"

	install -D -m 0755 libskype.so "${pkgdir}$(pkg-config --variable=plugindir purple)/libskype.so"
	install -D -m 0755 libskype_dbus.so "${pkgdir}$(pkg-config --variable=plugindir purple)/libskype_dbus.so"

	install -d -m 0755 "${pkgdir}/usr/share/empathy/icons"
	cp -r "${srcdir}/hicolor" "${pkgdir}/usr/share/empathy/icons/"
	install -D -m 0644 icons/16/skype.png "${pkgdir}/usr/share/pixmaps/pidgin/protocols/16/skype.png"
	install -D -m 0644 icons/22/skype.png "${pkgdir}/usr/share/pixmaps/pidgin/protocols/22/skype.png"
	install -D -m 0644 icons/48/skype.png "${pkgdir}/usr/share/pixmaps/pidgin/protocols/48/skype.png"
	install -D -m 0644 theme "${pkgdir}/usr/share/pixmaps/pidgin/emotes/skype/theme"
}
