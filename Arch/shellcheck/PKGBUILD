# Maintainer: Josh McGee <josh.s.mcgee@gmail.com>
# Contributor: Earnestly <zibeon@gmail.com>

pkgname=shellcheck
pkgver=0.3.3
pkgrel=1
pkgdesc="A shell script static analysis tool: Stable Release"
arch=(i686 x86_64)
url=http://www.shellcheck.net/
license=(AGPL3)
depends=('gmp')
makedepends=(
    'ghc'
    'haskell-json'
    'haskell-parsec'
    'haskell-quickcheck'
    'haskell-regex-base'
    'haskell-regex-compat')
provides=(shellcheck)
conflicts=(shellcheck-git)
source=("https://github.com/koalaman/shellcheck/archive/v$pkgver.tar.gz")
md5sums=('ed06fea3b4742399f8d6a2db361fc79b')

build() {
    cd "$pkgname-$pkgver"
    ghc -O9 -fforce-recomp --make shellcheck.hs
}

package() {
    cd "$pkgname-$pkgver"
    install -Dm755 shellcheck "$pkgdir/usr/bin/$pkgname"
}
