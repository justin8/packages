# Maintainer: Justin Dray <justin@dray.be>
# Maintainer: Daniel Egeberg <daniel.egeberg@gmail.com>
pkgname="sonarr"
pkgver="2.0.0.2175"
pkgrel=2
pkgdesc="PVR for newsgroup users. Formerly known as NZBDrone"
arch=(any)
url="http://www.sonarr.tv"
license=('GPL3')
depends=('mono' 'libmediainfo' 'sqlite')
optdepends=('sabnzbd: an NZB downloader'
            'nzbget: an NZB downloader'
	    'transmission: a torrent downloader'
	    'deluge: a torrent downloader')
install='sonarr.install'
provides='nzbdrone'
conflicts=('nzbdrone' 'nzbdrone-torrents')
changelog=
source=("http://update.nzbdrone.com/v2/sonarr/mono/NzbDrone.sonarr.${pkgver}.mono.tar.gz"
        "sonarr.sh"
        "sonarr.service"
	"sonarr.install")
noextract=()
md5sums=('8b71fb94f0f43c16396853b170e55ef8'
         'a7490c3f8d6c3a314b59f87f71086f1f'
         '41d8663e989a8db1fc4c6b81187a4d48'
         '6413a3db424de8d85a320c9e60ecac14')

package() {
    cd "$srcdir"

    install -d -m 755 "${pkgdir}/var/lib/sonarr"

    msg2 "Install Sonarr in /usr/lib"
    install -d -m 755 "${pkgdir}/usr/lib/sonarr"
    cp -dpr --no-preserve=ownership "${srcdir}/NzbDrone/"* "${pkgdir}/usr/lib/sonarr"

    msg2 "Install executable into /usr/bin"
    install -D -m755 "${srcdir}/sonarr.sh" "${pkgdir}/usr/bin/sonarr"

    msg2 "Install sonarr.service"
    install -D -m 644 "${srcdir}/sonarr.service" "${pkgdir}/usr/lib/systemd/system/sonarr.service"
}
