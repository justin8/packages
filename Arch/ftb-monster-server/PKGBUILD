# Maintainer: Justin Dray <justin@dray.be>

pkgname=ftb-monster-server
pkgver=1.0.9
pkgrel=1
pkgdesc="Feed the Beast Monster server. Spiritual successor to FTB Unleashed"
arch=('i686' 'x86_64')
url="http://www.feed-the-beast.com/"
license=('custom')
depends=('expect' 'java-runtime-headless' 'screen' 'unzip')
backup=("etc/conf.d/${pkgname/-server}")
options=(emptydirs)
install=install
source=(MonsterServer.zip::http://www.creeperrepo.net/direct/FTB2/18606e7928709218bd2a4386fb84ada9/modpacks%5EMonster%5E1_0_9%5EMonsterServer.zip
        bin
        service
	config)
noextract=('MonsterServer.zip')
md5sums=('9c8add8f1e9a3a7af69a4d1de5d85b35'
         '44060ddd515aa64f2acd1a2081100410'
         '21381b0f1d3bf356568a2b578deb646d'
         'bef2213c1448226d3840d1152b92a19a')
PKGEXT='.pkg.tar.gz'

build() {
	cd "$srcdir"
	unzip -qo "MonsterServer.zip" -d "$srcdir/$pkgname"
}

package() {
	cd "$srcdir"
	
	install -d "$pkgdir/srv/$pkgname"
	cp -r "$srcdir/$pkgname/"* "$pkgdir/srv/$pkgname/"
	install -Dm744 "$srcdir/bin" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 "$srcdir/service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
	install -Dm644 "$srcdir/config" "$pkgdir/etc/conf.d/${pkgname/-server}"
	rm -f "$pkgdir"/srv/$pkgname/ServerStart.{bat,sh}
}
