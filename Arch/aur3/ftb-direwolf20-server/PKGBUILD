# Maintainer: Justin Dray <justin@dray.be>

MCVER="1.7.10"
JARFILE="minecraft_server.${MCVER}.jar"
LAUNCHWRAPPER="net/minecraft/launchwrapper/1.11/launchwrapper-1.11.jar"

pkgname=ftb-direwolf20-server
pkgver=1.0.3
pkgrel=1
pkgdesc="Feed the Beast Direwolf20 server."
arch=('i686' 'x86_64')
url="http://www.feed-the-beast.com/"
license=('custom')
depends=('expect' 'java-runtime-headless' 'screen' 'unzip')
backup=("etc/conf.d/${pkgname/-server}.conf")
options=(emptydirs)
install="${pkgname}.install"
source=("Direwolf20Server-$pkgver.zip::http://www.creeperrepo.net/FTB2/modpacks%5Edirewolf20_17%5E1_0_3%5Edirewolf20_17-server.zip"
        "https://s3.amazonaws.com/Minecraft.Download/versions/1.7.10/minecraft_server.1.7.10.jar"
        "https://libraries.minecraft.net/net/minecraft/launchwrapper/1.11/launchwrapper-1.11.jar"
        "${pkgname}.sh"
        "${pkgname}.service"
        "${pkgname}.conf")
noextract=("Direwolf20Server-$pkgver.zip"
           "minecraft_server.1.7.10.jar"
	   "launchwrapper-1.11.jar")
md5sums=('26dc095b3cb5e19fbd7370fa0730e7c5'
         '3e8912b293b52bd54f879919bd6e4e44'
         '189acaecbe31eeb4366bca7ba17d7496'
         '26cc777c8aff409906c76ab90c33506d'
         '22574a505a06e50091340b7bd5fc92df'
         'f940980b530e41da4b7e0ac9b4f4cf08')
PKGEXT='.pkg.tar.gz'

prepare() {
	cd "$srcdir"
	unzip -qo "Direwolf20Server-$pkgver.zip" -d "$srcdir/$pkgname"
	sed -i 's/eula=.*/eula=true/g' "$pkgname/eula.txt"
}

package() {
	cd "$srcdir"
	install -d "$pkgdir/srv/$pkgname"
	cp -r "$srcdir/$pkgname/"* "$pkgdir/srv/$pkgname/"
	install -Dm744 "$srcdir/${pkgname}.sh" "$pkgdir/usr/bin/$pkgname"
	install -Dm644 "$srcdir/${pkgname}.service" "$pkgdir/usr/lib/systemd/system/$pkgname.service"
	install -Dm644 "$srcdir/${pkgname}.conf" "$pkgdir/etc/conf.d/${pkgname/-server}.conf"
	install -D "$srcdir/minecraft_server.1.7.10.jar" "$pkgdir/srv/$pkgname/minecraft_server.1.7.10.jar"
	install -D "$srcdir/launchwrapper-1.11.jar" "$pkgdir/srv/$pkgname/libraries/net/minecraft/launchwrapper/1.11/launchwrapper-1.11.jar"
	rm -f "$pkgdir"/srv/$pkgname/{ServerStart,FTBInstall}.{bat,sh}
}
