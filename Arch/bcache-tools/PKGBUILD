# Maintainer: Justin Dray <justin@dray.be>
# Contributor: Mikeserv

pkgname=bcache-tools
pkgver=1.0.7
pkgrel=1
pkgdesc="Userspace tools for bcache until bcache merges with either dm or md"
changelog=bcache-tools.changelog
arch=('i686' 'x86_64')
url="http://bcache.evilpiepirate.org/"
license=('GPL')
depends=('util-linux')
makedepends=('git')
install="${pkgname}.install"
source=("https://github.com/g2p/${pkgname}/archive/v${pkgver}.zip"
        'initcpio-arch.patch')
md5sums=('5fa28d545ec825ada4178f4066652d43'
         '208d7024340b8db3cf21325df40b6267')

build() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  patch -Np1 -i "${srcdir}/initcpio-arch.patch"
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"
  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/lib/udev/rules.d
  mkdir -p ${pkgdir}/usr/share/man/man8
  mkdir -p ${pkgdir}/usr/lib/initcpio/install

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
