# Maintainer: Justin Dray <justin@dray.be>
# Previous maintainer: bobpaul (bobpaul on archlinux forumsBoohbah <boohbah at gmail.com>)
# Contributor: Mikeserv

_gitname=bcache-tools
pkgname=${_gitname}-git
pkgver=0.r87.ba2c135
pkgrel=1
pkgdesc="Userspace tools for bcache until bcache merges with either dm or md"
changelog=bcache-tools-git.changelog
arch=('i686' 'x86_64')
url="http://bcache.evilpiepirate.org/"
license=('GPL')
makedepends=('git')
provides=('bcache-tools')
conflicts=('bcache-tools')
source=("git+https://github.com/g2p/${_gitname}.git"
        'initcpio-arch.patch')
md5sums=('SKIP'
         '540343e613f4623dc5a4e9be6bccf2a4')

# set _gitrev to a git revision (man gitrevisions) like a tag, a commit sha1
# hash or a branch name to build from this tree instead of master

# _gitrev=""
#_gitrev="89f11b135d1"   #Known Good rev (patches were built against this rev)

pkgver() {
  cd "${srcdir}/${_gitname}"
	# [optionally] reset to specified git revision
  [[ -n $_gitrev ]] && git reset --hard "$_gitrev"
  #git log -1 --format="%cd.g%h" --date=short | sed 's/-/./g'
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  cd "${srcdir}/${_gitname}"
  patch -Np1 -i "${srcdir}/initcpio-arch.patch"
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  mkdir -p ${pkgdir}/usr/bin
  mkdir -p ${pkgdir}/usr/lib/udev/rules.d
  mkdir -p ${pkgdir}/usr/share/man/man8
  mkdir -p ${pkgdir}/usr/lib/initcpio/{hooks,install}

  make DESTDIR="${pkgdir}" install
}

# vim:set ts=2 sw=2 et:
